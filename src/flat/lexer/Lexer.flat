package flat/lexer

import flat/io/File
import flat/log/Logger
import flat/stream/Stream

import flat/extensions/SyntaxStringFunctions

class {
  let static Logger log = Logger(Lexer.class)
  let static Char[] STATEMENT_END_CHARS = ['\n', '.', ';', '{', '}', '[', ']', '(', ')', '/', '*', '%', '=', ' ', ':']

  public lex(File file) => lex(file.createReadStream())

  public lex(Stream dataStream) -> Stream {
    let stream = Stream(true)

    dataStream.on<String>("data", (data) => {
      Lexer.log.trace("Received data from Stream with count: #{data.count}")
      let chunkStream = lex(data)

      chunkStream.on<Lexeme>("data", (lexeme) => {
        Lexer.log.trace("Received lexeme from Lexeme Stream: #{lexeme}")
        stream.emit("data", lexeme)$
      })$

      chunkStream.on<String>("error", (error) => {
        Lexer.log.trace("Received error from Lexeme Stream: #{error}")
        stream.emit("error", error)$
      })$

      chunkStream.on("close", {
        Lexer.log.trace("Lexeme Stream closed")
      })$
    })$

    dataStream.on<String>("error", (error) => {
      Lexer.log.trace("Received error from Stream: #{error}")
      stream.emit("error", error)$
    })$

    dataStream.on("close", {
      Lexer.log.trace("Stream closed")
      stream.emit("close")$
    })$

    return stream
  }

  public lex(String contents) -> Stream {
    let stream = Stream(true)

    var Int prevIndex = -1
    var Int index = contents.findCharOnTopLevel(STATEMENT_END_CHARS)

    var Int lineNumber = 1
    var Int column = 1

    while (index != -1) {
      let lexeme = createLexeme(contents, index, prevIndex, lineNumber, column)

      stream.emit("data", lexeme)$

      column += index - prevIndex
      lineNumber += lexeme.value.howMany('\n')

      let char = contents[index]

      match char {
        '\n' => {
          lineNumber++
          column = 1
        }
      }

      prevIndex = index
      index = contents.findCharOnTopLevel(STATEMENT_END_CHARS, start: index + 1)
    }

    if (let remainder = createLexeme(contents, index, prevIndex, lineNumber, column)) {
      stream.emit("data", remainder)$
    }

    stream.emit("close")$

    return stream
  }

  createLexeme(
    String contents,
    Int index,
    Int prevIndex,
    Int lineNumber,
    Int column
  ) -> Lexeme => null {
    let value = contents.substring(prevIndex + 1, index == -1 ? contents.count : index)
    let trimmedStart = value.trimStart()
    let trimmed = trimmedStart.trimEnd()
    let buffer = contents.substring(prevIndex, prevIndex + 1 + (value.count - trimmedStart.count))

    if (value.count > 0 || buffer.count > 0) {
      return Lexeme(
        buffer: buffer,
        value: trimmed,
        location: Lexeme.Location(
          lineNumber: lineNumber,
          column: column
        )
      )
    }
  }

  public static async consumeLexemeStream(Stream stream) => LexResult() {
    external {
      let resolve;
      let reject;

      const promise = new Promise((res, rej) => {
        resolve = res;
        reject = rej;
      });
    }

    let result = LexResult(Array())

    stream
      .on<Lexeme>("data", (data) => {
        result.lexemes.add(data)
      })
      .on<String>("error", (error) => {
        external {
          reject(#{Exception(error)});
        }
      })
      .on("close", {
        external {
          resolve(#{result});
        }
      })

    external {
      return promise;
    }
  }
}
